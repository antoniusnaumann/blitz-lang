fn parse_precedence(mut parser Parser, lhs ast::Expression, min_prec Int) Option(ast::Expression) {
    var left = lhs
    parser.mut.skip_newlines()

    while true {
        let peek_ = parser.peek()
       
        if peek_.kind == lbracket {
            parser.mut.expect(lbracket)
            parser.mut.skip_newlines()
            let index = try parser.mut.parse_expression()
            let end_bracket = try parser.mut.expect(rbracket)
            let span = left.span.merge(end_bracket.span)

            left = ast::Index(target: left, index:, span:)
            continue
        }

        let op = switch peek_.kind {
            ast::Operator {
                peek_.kind
            }
            else {
                break
            }
        }

        let prec = op.precedence()
        if prec.left < min_prec { break }

        parser.mut.tok()
        parser.mut.skip_newlines()

        switch op {
            dot {
                switch parser.mut.accept(mut_) {
                    some {
                        left = ast::Mut(expr: left, span: parser.span(), ufcs: true)
                        continue
                    }
                    none { }
                }
                switch parser.mut.accept(try_) {
                    some {
                        left = ast::Try(expr: left, span: parser.span(), ufcs: true)
                        continue
                    }
                    none { }
                }
                let member = parser.mut.parse_ident()
                let span = left.span.merge(parser.span(member.span))

                switch parser.peek().kind {
                    lparen {
                        left = parser.mut.parse_member_call(left, member)
                    }
                    path {
                        // UFCS with module namespace: expr.module::method(args)
                        parser.mut.tok()
                        let method = parser.mut.parse_ident()
                        left = parser.mut.parse_member_call_with_module(left, method, member)
                    }
                    else {
                        left = ast::Member(parent: left, member:, span:)
                    }
                }
            }
            else {
                let rhs = parser.mut.parse_expression_bp(prec.right)
                left = ast::BinaryOp(op:, left:, right: rhs, span: lhs.span.merge(rhs.span))
            }
        }
    }

    left
}

fn parse_expression(mut parser Parser) Option(ast::Expression) {
    parser.mut.parse_expression_bp(0)
}

fn parse_expression_bp(mut parser Parser, min_prec Int) Option(ast::Expression) {
    parser.mut.skip_newlines()
    let token = parser.peek()

    let lhs = switch token.kind {
        // TODO: handle prefix operators here
        for_ { parser.mut.parse_for() }
        while_ { parser.mut.parse_while() }
        if_ { parser.mut.parse_if() }
        switch_ { parser.mut.parse_switch() }
        return_ {
            parser.mut.tok()
            let peek = parser.peek()
            let span = token.span
            let empty_return = ast::Return(expr: ast::Block(statements: [], span:), span:)
            switch peek.kind {
                var_ { empty_return }
                let_ { empty_return }
                semicolon { empty_return }
                rbrace { empty_return }
                rbracket { empty_return }
                rparen { empty_return }
                newline { empty_return }
                else { ast::Return(expr: try parser.mut.parse_expression(), span: parser.span(span)) }
            }
        }
        continue_ { 
            parser.mut.tok()
            ast::Continue(span: parser.span(token))
        }
        break_ {
            parser.mut.tok()
            ast::Break(span: parser.span(token))
        }
        try_ {
            parser.mut.tok()
            ast::Try(expr: try parser.mut.parse_expression(), span: parser.span(token), ufcs: false)
        }
        assert_ {
            parser.mut.tok()
            ast::Assert(cond: try parser.mut.parse_expression(), span: parser.span(token), ufcs: false)
        }
        mut_ {
            parser.mut.tok()
            ast::Mut(expr: try parser.mut.parse_expression(), span: parser.span(token), ufcs: false)
        }
        lbrace { try parser.mut.parse_body() }
        ident {
            let span = token.span
            let name = parser.mut.parse_ident()
            switch parser.peek().kind {
                path {
                    // Qualified name: module::Type(...) or module::function(...)
                    parser.mut.tok()
                    switch parser.peek().kind {
                        type {
                            let ty = try parser.mut.parse_type_ident()
                            parser.mut.parse_constructor_with_module(ty, name)
                        }
                        ident {
                            let func_name = parser.mut.parse_ident()
                            parser.mut.parse_call_with_module(func_name, name)
                        }
                        else {
                            parser.mut.err("Expected type or function name after '::'")
                        }
                    }
                }
                lparen { parser.mut.parse_call(name) }
                else { name }
            }
        }
        type { parser.mut.parse_constructor() }
        lbracket { parser.mut.parse_list() }
        lparen { parser.mut.parse_group() }
        str { parser.mut.parse_string_lit() }
        char { parser.mut.parse_char_lit() }
        int { parser.mut.parse_int_lit() }
        float { parser.mut.parse_float_lit() }
        ast::UnaryOperator {
            parser.mut.tok()
            let inner = try parser.mut.parse_expression()
            ast::UnaryOp(op: it, expr: inner, span: parser.span(token))                   
        }
        else {
            print(token.kind)
            todo("Error handling: Illegal token for expression")
        }
    }

    let expr = parser.mut.parse_precedence(lhs, min_prec)
    switch parser.mut.consume_assignment() {
        some {
            let assignment = it
            let right = try parser.mut.parse_expression()
            
            // Desugar compound assignments: a += b becomes a = a + b
            let desugared_right = switch assignment {
                add { ast::BinaryOp(op: add, left: expr, right:, span: expr.span.merge(right.span)) }
                sub { ast::BinaryOp(op: sub, left: expr, right:, span: expr.span.merge(right.span)) }
                mul { ast::BinaryOp(op: mul, left: expr, right:, span: expr.span.merge(right.span)) }
                div { ast::BinaryOp(op: div, left: expr, right:, span: expr.span.merge(right.span)) }
                rem { ast::BinaryOp(op: rem, left: expr, right:, span: expr.span.merge(right.span)) }
                concat { ast::BinaryOp(op: concat, left: expr, right:, span: expr.span.merge(right.span)) }
                plain { right }
            }

            ast::Assignment(left: expr, right: desugared_right)
        }
        none { expr }
    }
}

union Assignment {
    add
    sub
    mul
    div
    rem
    concat
    plain
}

fn consume_assignment(mut parser Parser) Option(Assignment) {
    let token = try parser.mut.accept([add_assign, sub_assign, mul_assign, div_assign, rem_assign, concat_assign, assign])
    
    switch token.kind {
        add_assign { add }
        sub_assign { sub }
        mul_assign { mul }
        div_assign { div }
        rem_assign { rem }
        concat_assign { concat }
        assign { plain }
        else { panic("unreachable") }
    }
}

fn parse_for(mut parser Parser) Option(ast::For) {
    let kw = try parser.mut.expect(for_)
    let iter = try parser.mut.parse_expression()

    // TODO: implement implicit argument it
    try parser.mut.expect(pipe)
    let elem = try parser.parse_ident()
    try parser.mut.expect(pipe)

    let body = try parser.mut.parse_body()
    
    ast::For(iter:, elem:, body:, span: parser.span(kw))
}

fn parse_while(mut parser Parser) Option(ast::While) {
    let kw = try parser.mut.expect(while_)
    let cond = try parser.mut.parse_expression()
    let body = try parser.mut.parse_body()

    ast::While(cond:, body:, span: parser.span(kw))
}

fn parse_if(mut parser Parser) Option(ast::If) {
    let kw = try parser.mut.expect(if_)
    let cond = try parser.mut.parse_expression()
    let body = try parser.mut.parse_body()

    ast::If(cond:, body:, span: parser.span(kw))
}

fn parse_switch(mut parser Parser) Option(ast::Switch) {
    let token = try parser.mut.expect(switch_)
    let expr = try parser.mut.parse_expression()
    try parser.mut.expect(lbrace)
    parser.mut.skip_newlines()

    var cases = []
    while !parser.has(rbrace) {
        let case_start = parser.span()
        let label = try parser.mut.parse_switch_label()
        parser.mut.skip_newlines()
        let body = try parser.mut.parse_body()
        parser.mut.skip_newlines()

        cases ++= ast::SwitchCase(label:, body:, span: parser.span(case_start))
    }
    parser.mut.expect(rbrace)

    ast::Switch(cond: expr, cases:, span: parser.span(token))
}

fn parse_switch_label(mut parser Parser) Option(ast::SwitchLabel) {
    switch parser.peek().kind {
        ident {
            // Check if this is a qualified type: ident::Type
            let name = try parser.mut.parse_ident()
            switch parser.peek().kind {
                path {
                    // Qualified type in switch label: module::Type
                    parser.mut.tok()
                    var ty = try parser.mut.parse_type()
                    ty.module = name
                    ty
                }
                else { name }
            }
        }
        type { try parser.mut.parse_qualified_type() }
        str { try parser.mut.parse_string_lit() }
        int { try parser.mut.parse_int_lit() }
        float { try parser.mut.parse_float_lit() }
        char { try parser.mut.parse_char_lit() }
        else_ {
            parser.mut.tok()
            default
        }
        else { parser.mut.err("Unexpected token for switch label!") }
    }
}

fn parse_member_call(mut parser Parser, target ast::Expression, method ast::Ident) Option(ast::Call) {
    var call = parser.mut.parse_call(method)
    call.ufcs = true
    call.args = [ast::CallArg(label: none, init: target)] ++ call.args
    call
}

fn parse_member_call_with_module(mut parser Parser, target ast::Expression, method ast::Ident, mod ast::Ident) Option(ast::Call) {
    var call = parser.mut.parse_call_with_module(method, mod)
    call.ufcs = true
    call.args = [ast::CallArg(label: none, init: target)] ++ call.args
    call
}

fn parse_call(mut parser Parser, name ast::Ident) Option(ast::Call) {
    let token = parser.peek()
    let args = parser.mut.parse_args() 
    
    ast::Call(name:, module: none, args:, ufcs: false, span: parser.span(token))
}

fn parse_call_with_module(mut parser Parser, name ast::Ident, mod ast::Ident) Option(ast::Call) {
    let token = parser.peek()
    let args = parser.mut.parse_args()

    ast::Call(name:, module: mod, args:, ufcs: false, span: parser.span(token))
}

fn parse_constructor(mut parser Parser) Option(ast::Constructor) {
    let token = parser.peek()
    let ty = try parser.mut.parse_type_ident()
    let args = try parser.mut.parse_args()

    ast::Constructor(type: ty, args:, ufcs: false, span: parser.span(token))
}

fn parse_constructor_with_module(mut parser Parser, ty ast::Type, mod ast::Ident) Option(ast::Constructor) {
    let token = parser.peek()
    var qualified_ty = ty
    qualified_ty.module = mod
    let args = try parser.mut.parse_args()

    ast::Constructor(type: qualified_ty, args:, ufcs: false, span: parser.span(token))
}

fn parse_args(mut parser Parser) Option(List(ast::CallArg)) {
    try parser.mut.expect(lparen)
    var args = []

    while !parser.has(rparen) {
        let param = parser.peek()
        switch param.kind {
            ident {
                switch parser.peek(2).kind {
                    colon {
                        let label = try parser.mut.parse_ident()
                        try parser.mut.expect(colon)
                        let init = switch parser.peek().kind {
                            comma { label }
                            rparen { label }
                            else { try parser.mut.parse_expression() }
                        }
                        args ++= ast::CallArg(label:, init:)
                    }
                    else {
                        args ++= ast::CallArg(label: none, init: try parser.mut.parse_expression())
                    }
                }
            }
            else {
                let init = try parser.mut.parse_expression()
                args ++= ast::CallArg(label: none, init:)
            }
        }
        parser.mut.accept(comma)

        parser.peek()        
    }
    parser.mut.expect(rparen)

    args
}

fn parse_list(mut parser Parser) Option(ast::List_) {
    let start = try parser.mut.expect(lbracket)
    var elems = []

    while !parser.has(rbracket) {
        parser.mut.skip_newlines()
        elems ++= parser.mut.parse_expression()
        parser.mut.accept(comma)
        parser.mut.skip_newlines()
    }
    parser.mut.expect(rbracket)

    ast::List_(elems:, span: parser.span(start))
}

fn parse_group(mut parser Parser) Option(ast::Expression) {
    let token = try parser.mut.expect(lparen)
    let expr = try parser.mut.parse_expression()
    try parser.mut.expect(rparen)

    ast::Group(expr:, span: parser.span(token))
}

fn parse_string_lit(mut parser Parser) Option(ast::Lit(String)) {
    let token = parser.mut.expect(str)
    let value = parser.source.substr(token.span.range.begin + 1, token.span.range.until - 1)

    ast::Lit(value:, span: parser.span(token))
}

fn parse_char_lit(mut parser Parser) Option(ast::Lit(Rune)) {
    let token = parser.mut.expect(char)
    let chars = parser.source.substr(token.span.range.begin + 1, token.span.range.until - 1).chars()
    let value = switch chars.len() {
        1 { chars[0] }
        2 {
            if chars[0] != '\\' {
                parser.mut.err("Invalid char length")
                return none
            }

            switch chars[1] {
                'n' { '\n' }
                'r' { '\r' }
                't' { '\t' }
                '0' { '\0' }
                '\\' { '\\' }
                '\'' { '\'' }
                'x' { todo("Hex lit") }
                'u' { todo("Unicode lit") }
                '(' { todo("Format strings") }
                else {
                    parser.mut.err("Invalid escape sequence")
                    return none
                }
            }
        }
        else {
            parser.mut.err("Invalid char length")
            return none
        }
    }

    ast::Lit(value:, span: parser.span(token))
}

fn parse_int_lit(mut parser Parser) Option(ast::Lit(Int)) {
    let token = parser.mut.expect(int)
    var index = token.span.range.begin
    var number = 0

    while index < token.span.range.until {
        number *= 10
        number += switch parser.source[index] {
            // TODO: replace with ASCII math
            '0' { 0 }
            '1' { 1 }
            '2' { 2 }
            '3' { 3 }
            '4' { 4 }
            '5' { 5 }
            '6' { 6 }
            '7' { 7 }
            '8' { 8 }
            '9' { 9 }
        }
        index += 1
    }

    ast::Lit(value: number, span: parser.span(token))
}

fn parse_float_lit(mut parser Parser) Option(ast::Lit(Float)) {
    todo("parse float lit")
}

fn parse_body(mut parser Parser) Option(ast::Block) {
    try parser.mut.expect(lbrace)
    let first = parser.peek()
    var body = []
    while !parser.has(rbrace) {
        let statement = try parser.mut.parse_statement()
        body ++= statement
        parser.mut.accept(semicolon)
        parser.mut.skip_newlines()
    }
    parser.mut.expect(rbrace)

    ast::Block(statements: body, span: parser.span(first))
}
